/*
 * Copyright (C) 2014 Monofraps
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.

 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.

 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'net.monofraps'
version = '1.0'

gradle.taskGraph.whenReady { taskGraph ->
    if (!taskGraph.hasTask(release)) {
        version += '-SNAPSHOT'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.11'

    compile 'org.apache.httpcomponents:httpclient:4.3.1'
    compile 'org.apache.httpcomponents:httpmime:4.3.1'
    compile 'org.apache.commons:commons-lang3:3.2.1'
    compile 'com.google.guava:guava:15.0'
    compile 'com.google.code.gson:gson:2.2.4'

    compile gradleApi()
    compile localGroovy()
}

task javadocsJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

signing {
    sign configurations.archives
}

artifacts {
    archives jar

    archives javadocsJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

task snapshot(dependsOn: 'uploadArchives') << {
    println 'Snapshot deploy!'
}

task release(dependsOn: 'uploadArchives') << {
    println 'Release deploy!'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            }

            pom.project {
                name 'GradleCurse'
                packaging 'jar'
                description 'GradleCurse is a gradle plugin which allows to deploy build artifacts to the Curse network.'
                url 'http://gradlecurse.monofraps.net'

                scm {
                    url 'scm:git@github.com:Monofraps/GradleCurse.git'
                    connection 'scm:git@github.com:Monofraps/GradleCurse.git'
                    developerConnection 'scm:git@github.com:Monofraps/GradleCurse.git'
                }

                licenses {
                    license {
                        name 'GNU GENERAL PUBLIC LICENSE Version 3'
                        url 'http://www.gnu.org/licenses/gpl.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'monofraps'
                        name 'Nicolas Ristock'
                    }
                }
            }
        }
    }
}
